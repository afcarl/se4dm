#!/usr/bin/env awk -f

BEGIN {
     Color4="brown"
     Color2="teal"  
     Color1="DarkBlue"
     Color3="f79a32"
     Words = "def "      \
             " for in int if or len  True False str lambda and not "\
             " class else while print import " \
             " sprintf rand :  switch"          \
             " BEGIN END next continue  "        \
             " return length "
     split(Words,Tmp," ")
     for(Word in Tmp) {
        Pat = Pat Sep "\\y" Tmp[Word] "\\y"
        Sep = "|"
     }
     Pat = "(" Pat ")"
}

function pretty(str) {
      # line++
      # pre=""
      # if (str !~ /^[ \t]*$/)
      #   pre= sprintf("<font color=#BBB>%5d:</font>   ",line)
       gsub(/[\+=\*-/<>^]/,
            "<font color=black>&</font>",str)
       gsub(/<[\/]?code>/,"",str)
       gsub(Pat,      "<font color="Color1">&</font>",str) 
       gsub(/"[^"]*"/,"<font color="Color2">&</font>",str)
       gsub(/#.*/,   "<font color="Color3">&</font>",str)
       str = gensub(/(\y[_a-zA-Z0-9]+\y)\(/,
           "<font color="Color4">\\1</font>(","g",str)
       return  str
}

/code>/ {
     In = 1 - In
     print $0
     next
}

In    { $0 = pretty($0) }
      { print  $0 } 
  END { if (! Out) print "</pre>" }

